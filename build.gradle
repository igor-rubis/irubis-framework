/**
 * Copyright 2016 Igor Rubis
 * Licensed under the Apache License, Version 2.0
 */

group 'com.irubis_framework'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'

repositories {
    mavenCentral()
}

configurations {
    agent
}

dependencies {
    agent "org.aspectj:aspectjweaver:1.8.13"
    compile     group: 'org.hamcrest',              name: 'hamcrest-core',          version: '1.3'
    compile     localGroovy()
    compile     group: 'org.seleniumhq.selenium',   name: 'selenium-java',          version: '3.9.1'
    compile     group: 'ru.yandex.qatools.allure',  name: 'allure-junit-adaptor',   version: '1.5.4'
    compile     group: 'ru.yandex.qatools.ashot',   name: 'ashot',                  version: '1.5.2'
    compile     group: 'com.github.groovy-wslite',  name: 'groovy-wslite',          version: '0.8.0'
    compile     group: 'io.netty',                  name: 'netty-codec-http',       version: '4.0.15.Final'
    compile     group: 'com.google.appengine',      name: 'appengine-api-1.0-sdk',  version: '1.3.1'
    compile     group: 'io.github.bonigarcia',      name: 'webdrivermanager',       version: '1.5.0'
    compile     group: 'com.machinepublishers',     name: 'jbrowserdriver',         version: '0.17.0'

    testCompile files("${projectDir}/src/main/groovy")
}

def allureExecutable = 'D:\\Downloads\\allure-2.5.0\\bin\\allure.bat'

task openAllureReport(type: Exec) {
    executable allureExecutable
    args('open')
}

task generateAllureReport(type: Exec) {
    executable allureExecutable
    args('generate', 'build/reports/allure', '--clean')
    finalizedBy openAllureReport
}

task runAnyTest(type: Test) {
    dependsOn(clean)
    include "**/${System.getProperty('currentTest', '*')}.*"
}

task splitTests(dependsOn: 'classes', type: JavaExec) {
    main = 'com.irubis_framework.helpers.scripts.SplitTests'
    classpath = sourceSets.main.runtimeClasspath
    systemProperties(System.properties as Map)
}

new HashSet(tasks.withType(Test)).each { task ->
    task.dependsOn(clean)
    task.ignoreFailures(true)
    task.useJUnit()
    task.systemProperties(System.properties)
    task.jvmArgs "-javaagent:${configurations.agent.singleFile}"
    task.testLogging.showStandardStreams = System.getProperty('debug', 'false').toBoolean()
    task.testLogging.exceptionFormat = 'full'
    task.testLogging.showExceptions = true
    task.maxHeapSize = "2048m"

    task.finalizedBy(generateAllureReport)
}
